use crate::ast::parser_builder::*;

grammar(symbols: &mut SymbolsTable);

pub Prog: Ast = {
    <Expr>,
    <Assignation>,
}

Assignation: Ast = {
    "let" <Identifier> "=" <Expr> => parse_assignment(symbols, <>),
}

Expr: Ast = {
    #[precedence(level="1")]
    Atom,

    #[precedence(level="2")] #[assoc(side="left")]
    <Expr> <OpCodeFactor> <Expr> => parse_op(<>),
    #[precedence(level="3")] #[assoc(side="left")]
    <Expr> <OpCodeTerm> <Expr> => parse_op(<>),
};

OpCodeTerm: OpCode = {
    "+" => Add,
    "-" => Sub,
}

OpCodeFactor: OpCode = {
    "*" => Mul,
    "/" => Div,
}

Atom: Ast = {
    Value => Leaf(<>),
    <Identifier> => Variable(<>),
    "(" <Expr> ")",
}

Value: Value = {
    Num,
    String,
}

Num: Value = r"[0-9]+" => Value::parse_int(<>);
String: Value = r#""([^\\"]|(\\"))*""# => Value::parse_str(<>);
Identifier: Identifier = r"[a-zA-Z_]\w*" => Identifier::from(<>);
